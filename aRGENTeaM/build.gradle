buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.11.+'
    }
}

apply plugin: 'android'

repositories {
    mavenCentral()
}

dependencies {
    compile project(':libraries:AmbilWarna')
    compile files('libs/libGoogleAnalyticsServices.jar')
    compile 'com.android.support:appcompat-v7:19.1.0'
    compile 'com.android.support:support-v4:19.1.0'
    compile 'commons-net:commons-net:3.3@jar'
    compile 'org.jsoup:jsoup:1.7.3@jar'
    compile 'joda-time:joda-time:2.3@jar'
    compile 'com.google.android.gms:play-services:4.4.52'
    compile 'com.squareup.okhttp:okhttp:1.3.0'
    compile 'ch.acra:acra:4.5.0'

}


def getVersionCode = { ->
    try {
        def code = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'log', '--oneline'
            standardOutput = code
        }
        return code.toString().split("\n").size()
    }
    catch (ignored) {
        return -1;
    }
}

android {
    compileSdkVersion 19
    buildToolsVersion '20'

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    defaultConfig {
        versionCode 21000 + getVersionCode()
        versionName "2.1." + getVersionCode()
    }

    lintOptions {
        abortOnError false
    }
}
